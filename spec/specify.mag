import magpie.reflect

var _totalTests = 0
var _totalFailures = 0

var _currentTestSuite = nothing
var _startTimeMs = 0

def startTests()
    _totalTests = 0
    _totalFailures = 0
    _currentTestSuite = nothing
    print("Executing specification...")
    _startTimeMs = currentTime()
end

def endTests()
    var endTimeMs = currentTime()
    print()
    print("Passed " + (_totalTests - _totalFailures) + " out of " +
        _totalTests + " tests in " + (endTimeMs - _startTimeMs) + "ms.")
end

// TODO(bob): Need function types.
def specify(description is String, block)
    _currentTestSuite = _TestSuite new(_name: description)
    block call(_currentTestSuite)
end

defclass _TestSuite
    var _name        is String
    var _currentTest = ""
    var _failedTest  = false
end

// TODO(bob): Need function types.
def (this is _TestSuite) should(description is String, block)
    // TODO(bob): Should not need "this " before setting fields.
    this _currentTest = description
    var testsBefore = _totalTests
    this _failedTest = false

    do
        block call()
    catch error then
        fail("Unexpected error: " + error class)
    end

    if _totalTests == testsBefore then
        print("NONE: " + this _name + " should " + this _currentTest + ".")
    end
end

def (this is _TestSuite) _pass()
    this _advance()
end

def (this is _TestSuite) _advance()
    _totalTests = _totalTests + 1
end

def (this is _TestSuite) _fail(message is String)
    if not(this _failedTest) then
        print("FAIL: " + this _name + " should " + this _currentTest + ".")
    end
    this _advance()
    _totalFailures = _totalFailures + 1
    print("      " + message)
    this _failedTest = true
end

def fail(message is String)
    _currentTestSuite _fail(message)
end

def (this) shouldEqual(expected, message is String)
    if this != expected then
        _currentTestSuite _fail(message)
    else
        _currentTestSuite _pass()
    end
end

def (this) shouldEqual(expected)
    this shouldEqual(expected, "Expected " + expected + " but was " + this)
end

def (this) shouldBe(expected is Class, message is String)
    if this is?(expected) then
        _currentTestSuite _pass()
    else
        _currentTestSuite _fail(message)
    end
end

def (this) shouldBe(expected is Class)
    this shouldBe(expected, "Expected class " + expected + " but was " + this class)
end

// Verifies that a function throws an error of the given class. Will execute the
// function.
def (this is Function) shouldThrow(errorClass is Class)
    var caught = false
    do
        this call()
    catch err is Error then
        err shouldBe(errorClass, "Should have thrown an error of class " +
            errorClass + " but got an error of class " + err class + ".")
        caught = true
    end
    caught shouldEqual(true, "Should have thrown an error of class " +
        errorClass + " but no error was thrown.")
end

// Verifies that a function does not throw an error. Will execute the function.
def (this is Function) shouldNotThrow()
    var caught = nothing
    do
        this call()
    catch err is Error then
        caught = err
    end
    caught shouldEqual(nothing, "Should not have thrown an error but threw " +
        "an error of class " + caught class + ".")
end
