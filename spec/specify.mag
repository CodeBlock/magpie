var totalTests = 0
var totalFailures = 0

var currentTestSuite = nothing

var startTests(->)
    totalTests = 0
    totalFailures = 0
    currentTestSuite = nothing
    print("Executing specification...")
end

var endTests(->)
    print("")
    print("Passed " + (totalTests - totalFailures) + " out of " +
        totalTests + " tests.")
end

var specify(description String, block Function(Nothing, Nothing) ->)
    var suite = TestSuite new(description)
    block(suite)
    
    totalTests = totalTests + suite tests
    totalFailures = totalFailures + suite failures
end

class TestSuite
    this(name)
        this name = name
        currentTestSuite = this
        currentTest = ""
        tests = 0
        failures = 0
    end
    
    should(description String, block Function(Nothing, Nothing) ->)
        currentTest = description
        var testsBefore = tests
        var failuresBefore = failures
        block()
        if tests == testsBefore then
            print("NONE: " + name + " should " + currentTest + ".")
        end
    end
    
    fail(message String ->)
        if failures == 0 then
            print("FAIL: " + name + " should " + currentTest + ".")
        end
        tests = tests + 1
        failures = failures + 1
        print("      " + message)
    end
    
    pass()
        tests = tests + 1
    end
    
    name String
    currentTest String
    tests Int
    failures Int
end

def Object shouldBe(expected, message)
    if this != expected then
        if message == nothing then
            message = "Expected " + expected + " but was " + this
        end
        // TODO(bob): Exceptions would be good here.
        currentTestSuite fail(message)
    else
        currentTestSuite pass
    end
end

def Object expectCheckError(block)
    (Runtime checkFunction(block) count > 0) shouldBe(true,
        "Should have a check error but did not.")
end

def Object expectCheckOK(block)
    Runtime checkFunction(block) count shouldBe(0,
        "Should not have any check errors but did.")
end
