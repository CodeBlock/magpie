import magpie.syntax
import spec.specify

specify("A quote expression") with
    it should("return a BoolExpression when containing a boolean literal") with
        { true } shouldBe(BoolExpression)
        { true } value shouldEqual(true)
        { false } value shouldEqual(false)
    end

    it should("return a IntExpression when containing an int literal") with
        { 1 } shouldBe(IntExpression)
        { 2 } value shouldEqual(2)
        { 3 } value shouldEqual(3)
    end

    it should("return a NothingExpression when containing nothing") with
        { nothing } shouldBe(NothingExpression)
    end

    it should("return a StringExpression when containing a string literal") with
        { "a" } shouldBe(StringExpression)
        { "b" } value shouldEqual("b")
        { "c" } value shouldEqual("c")
    end
end

specify("An unquote in a quote") with
    it should("insert the value of a named identifier into the quote") with
        var foo = { b a }
        var result = { `foo c }
        result name shouldEqual("c")
        result argument name shouldEqual("a")
        result argument argument name shouldEqual("b")
    end

    it should("allow a parenthesized expression after the backtick") with
        def make(value)
            IntExpression new(value: value)
        end
        var result = { `(make(123)) method }
        result name shouldEqual("method")
        result argument value shouldEqual(123)
    end

    it should("wrap an unquoted nothing in a NothingExpression") with
        { `(nothing) } shouldBe(NothingExpression)
    end

    it should("wrap an unquoted bool in a BoolExpression") with
        { `(true) } shouldBe(BoolExpression)
        { `(true) } value shouldEqual(true)
    end

    it should("wrap an unquoted int in an IntExpression") with
        { `(123) } shouldBe(IntExpression)
        { `(123) } value shouldEqual(123)
    end

    it should("wrap an unquoted string in a StringExpression") with
        { `("foo") } shouldBe(StringExpression)
        { `("foo") } value shouldEqual("foo")
    end
end
