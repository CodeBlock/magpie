import spec.specify

specify("An 'if then' expression") with
    it should("evaluate the 'then' expression if the condition is true") with
        var evaluated = false
        if true then evaluated = true
        evaluated shouldEqual(true)

        evaluated = false
        if false then evaluated = true
        evaluated shouldEqual(false)
    end

    it should("return the 'then' expression if the condition is true") with
        (if true then "then") shouldEqual("then")
    end

    it should("return nothing if the condition is non-true") with
        (if false then "then") shouldEqual(nothing)
    end

    it should("create a local scope for the 'if' expression that is visible " +
              "to the 'then' expression") with
        val a = "outside"
        var b = "outside"
        if val a = "inside" then b = a
        a shouldEqual("outside")
        b shouldEqual("inside")
    end

    it should("create a local scope for the 'then' expression") with
        val a = "outside"
        if true then val a = "inside"
        a shouldEqual("outside")
    end

    it should("allow a block for the 'if' expression") with
        var a = 0
        if
            a = a + 1
            a = a + 2
            true
        then
            true
        end
        a shouldEqual(3)
    end

    it should("allow a block for the 'then' expression") with
        var a = 0
        if true then
            a = a + 1
            a = a + 2
        end
        a shouldEqual(3)
    end
end

specify("An 'if then else' expression") with
    it should("evaluate the 'then' expression if the condition is true") with
        var evaluated = false
        if true then evaluated = true else "ignored"
        evaluated shouldEqual(true)

        evaluated = false
        if false then evaluated = true else "ignored"
        evaluated shouldEqual(false)
    end

    it should("evaluate the 'else' expression if the condition is false") with
        var evaluated = false
        if true then "ignored" else evaluated = true
        evaluated shouldEqual(false)

        evaluated = false
        if false then "ignored" else evaluated = true
        evaluated shouldEqual(true)
    end

    it should("return the 'then' expression if the condition is true") with
        (if true then "then" else "else") shouldEqual("then")
    end

    it should("return the 'else' expression if the condition is non-true") with
        (if false then "then" else "else") shouldEqual("else")
    end

    it should("create a local scope for the 'if' expression that is visible " +
              "to the 'else' expression") with
        val a = "outside"
        var b = "outside"
        if val a = "" then "ignored" else b = a
        a shouldEqual("outside")
        b shouldEqual("")
    end

    it should("create a local scope for the 'else' expression") with
        val a = "outside"
        if false then "ignored" else val a = "shadow"
        a shouldEqual("outside")
    end

    it should("allow a block for the 'if' expression") with
        var a = 0
        if
            a = a + 1
            a = a + 2
            true
        then
            true
        else "ignored"
        a shouldEqual(3)
    end

    it should("allow a block for the 'then' expression") with
        var a = 0
        if true then
            a = a + 1
            a = a + 2
        else "ignored"
        a shouldEqual(3)
    end

    it should("allow a block for the 'else' expression") with
        var a = 0
        if false then "ignored" else
            a = a + 1
            a = a + 2
        end
        a shouldEqual(3)
    end

    it should("not associate an 'else' on the next line with a previous " +
              "single-line 'then'") with
        (if true then
            if false then "then"
        else
            "else"
        end) shouldEqual(nothing)
    end
end