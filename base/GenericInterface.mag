class GenericInterface
    shared def new(name String, typeParams List(String) ->
                   GenericInterface)
        construct(name: name, typeParams: typeParams)
    end
    
    def call(typeArgs -> Interface)
        // See if we've already instantiated it first.
        var typeArgString = typeArgs string
        for instance = instances do
            if instance _0 == typeArgString then return instance _1
        end
        
        var instance = Interface new(name)
        
        // Add it to the cache immediately. This is necessary to be able to
        // instantiate recursive interfaces.
        instances add(typeArgString, instance)
        
        for member = members do
            var type = fn(-> Type) member memberType(typeArgs)
            instance members add(Member new(name: member name,
                memberType: type))
        end
        instance
    end
    
    get string String =
        var result = name + "["
        result = result + String join(typeParams)
        result + "]"
    end
    
    def declareMethod(name String, type ->)
        members add(GenericMember new(name: name, memberType: type))
    end
    
    def declareGetter(name String, type ->)
        members add(GenericMember new(name: name, memberType: type))
    end

    var name       String
    var typeParams List(String)
    var members    List(GenericMember) = Array new()
    // TODO(bob): Use a map instead. Don't key off string either.
    var instances  List(String, Interface) = Array new()
end

class GenericMember
    var name       String
    // TODO(bob): Dynamic because it will be a function that takes an unknown
    // number of parameters based on the number of type parameters in the
    // generic type.
    var memberType Dynamic
end
