// WARNING. This file and the files it imports bootstrap low-level things like
// the type system and basic operators like == and <. The order that code is
// evaluated here is significant, and subtle.

// TODO(bob): Move somewhere better.
class BreakParser
    def parse(parser MagpieParser -> Expression)
        parser consume("break")
        BreakExpression new()
    end
end

MagpieParser registerKeyword("break", BreakParser new())

import("Array.mag")
import("Class.mag")
import("Function.mag")
import("Int.mag")
import("GenericInterface.mag")
import("Interface.mag")
import("Record.mag")
import("String.mag")
import("Tuple.mag")
import("Type.mag")
import("OrType.mag")
import("Error.mag")
import("Object.mag")
import("Stringable.mag")
import("Trueable.mag")

import("Iterator.mag")
import("List.mag")

interface Any
end

// TODO(bob): Hackish. Need to figure out what the semantics for this should be.
def ==(left, right -> Bool)
    // Must be equal if they're the same object.
    if Reflect same?(left, right) then return true
    
    // Must be the same class.
    var leftClass = Reflect getClass(left)
    var rightClass = Reflect getClass(right)
    if Reflect same?(leftClass, rightClass) not then return false
    
    // Leave it up to the class to decide.
    let equalFunc = leftClass equal? then
        var equal = equalFunc(left, right)
        unsafecast[Bool](equal)
    else
        // Class doesn't provide an equality function, and they aren't the
        // same object, so default to not equal.
        false
    end
end

def !=(left, right -> Bool) (left == right) not

def <(left, right -> Bool)
    // Leave it up to the class to decide.
    let lessFunc = Reflect getClass(left) lessThan? then
        var less = lessFunc(left, right)
        unsafecast[Bool](less)
    else
        false
    end
end

def >(left, right -> Bool)
    (left < right) not and left != right
end

def <=(left, right -> Bool)
    (left < right) or left == right
end

def >=(left, right -> Bool)
    (left < right) not
end

def +(left Int, right Int -> Int)
    Int add(left, right)
end

// Add an arrow operator to make function types out of other types:
def =>(left Type, right Type -> Type) FunctionType new(left, right, false)

import("syntax/init.mag")
