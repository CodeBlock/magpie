// A record of Types is itself a Type.
extend class Record
    def assignsFrom?(other Type -> Bool)
        // As long as the other type has assignable getters for all of the
        // record's fields, we're good.
        for field = fields do
            // TODO(bob): Get rid of these when tuple fields are typed.
            var name = field _0 expecting[String]
            var fieldType = field _1 expecting[Type]
            if fieldType assignsFrom?(other getMemberType(name)) not then
                return false
            end
        end
        
        true
    end

    def getMemberType(name String -> Type)
        // Each field in a record type is a getter of that type.
        if hasField?(name) then
            getField(name)
        else
            // Not a field, so just fallback to what's built in to Record.
            Record getMemberType(name)
        end
    end
    
    shared def equal?(left Record, right Record -> Bool)
        if left fields count != right fields count then return false
        
        for leftField = left fields
        for rightField = right fields do
            if leftField != rightField then return false
        end
        true
    end
    
    get string String =
        var result = ""
        for field = fields do
            if result count > 0 then result = result ~ " "
            result = result ~ field _0 ~ ": " ~ field _1
        end
        result
    end
end
