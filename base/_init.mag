def (this String) string -> this
def (nothing)     string -> "nothing"
def (true)        string -> "true"
def (false)       string -> "false"
def (this Class)  string -> name

def print()            -> prints("\n")
def print(text String) -> prints(text +("\n"))
def print(any)         -> print(any string)

def not(true)  -> false
def not(false) -> true

// By default, == just does identity comparison.
def (this) ==(other) -> this sameAs?(other)

def (this) !=(other) -> not(this == other)

// Make + default to string concatenation. We'll only hit this if both
// arguments are not Ints.
def (this) +(other) -> this string +(other string)

// Truthiness.
def (this)        true? -> true
def (this Bool)   true? -> this
def (this Int)    true? -> this != 0
def (this String) true? -> this count > 0
def (nothing)     true? -> false
