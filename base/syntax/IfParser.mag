// Keyword parser for "if" expressions.
class IfParser
    def parse(parser MagpieParser, token Token -> Expression)
        // Parse the condition.
        var condition, _ = parser parseBlock("then")

        // Parse the then body.
        parser consume("then")
        var thenExpr, endToken = parser parseBlock("else", "end")

        // Parse the else body.
        var consumedEnd? =
            Reflect same?(endToken, nothing) not and endToken keyword?("end")
        end

        var elseExpr =
            match consumedEnd? not and parser matchKeyword("else")
                case true then parser parseEndBlock()
                else { nothing }
            end
        end

        {
            match `condition true?
                case true then `thenExpr
                else `elseExpr
            end
        }
    end
end

MagpieParser registerPrefixParser("if", IfParser new())
MagpieParser reserveWord("else")

// TODO(bob): Put this somewhere appropriate...
MagpieParser reserveWord("end")
