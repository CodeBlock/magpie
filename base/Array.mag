extend class Array
    get string String =
        var text = "["
        for i = 0 until(count) do
            text = text ~ this(i) string
            if i < (count - 1) then text = text ~ ", "
        end
        text ~ "]"
    end

    get last = this(count - 1)
end

def shared Array new() Array of()

def Array iterate() ArrayIterator new(this)

def Array copy()
    var result = Array new()
    for item = this do result add(item)
    result
end


def Array unique()
    var result = Array new()
    for item = this do
        if result count == 0 then
            result add(item)
        else if result(-1) != item then result add(item)
    end

    result
end

def Array contains?(item)
    for element = this do
        if item == element then return true
    end
    false
end

def Array first(predicate)
    for element = this do
        if predicate(element) then return element
    end
    nothing
end

def Array swap(a Int, b Int)
    // TODO(bob): Bounds check
    var temp = this(a)
    this(a) = this(b)
    this(b) = temp
end

class ArrayIterator
    get current = array(index)

    var array Array
    var index = -1
end

def shared ArrayIterator new(array Array)
    construct(array: array)
end


def ArrayIterator next()
    index = index + 1
    index < array count
end

