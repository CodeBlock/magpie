// Here's a couple of different ways to define the Fibonacci sequence in Magpie.

// A basic recursive solution using pattern-matching.
def fibr(n is Int)
    match n
        case 0 then 0
        case 1 then 1
        else fibr(n - 2) + fibr(n - 1)
    end
end

// An iterative solution.
def fibi(n is Int)
    if n <= 1 then return n

    var a, b = 0, 1
    for i = 2 to(n) do
        // TODO: Once destructuring assignment is working, can just be:
        // a, b = b, a + b
        var temp = a
        a = b
        b = temp + b
    end

    b
end

// Using multimethods to specialize on the degenerate cases.
def fibm(n is Int)
    fibm(n - 2) + fibm(n - 1)
end

def fibm(0)
    0
end

def fibm(1)
    1
end

for i = 0 to(20) do
    print(i + ": " + fibm(i))
end
