// Access variable in an outer scope.
do
    val a = "access"
    (fn() print(a)) call // expect: access
end

// Assign a variable in an outer scope.
do
    var a = "before"
    fn()
        print(a) // expect: before
        a = "after"
        print(a) // expect: after
    end call
    print(a) // expect: after
end

// Access closed over variable after it goes out of scope.
do
    var func = do
        var outer = "outer"
        fn() print(outer)
    end

    func call // expect: outer
end

// Access variable from distant outer scope.
do
    var f = nothing
    fn()
        val outer1 = "outer1"
        fn()
            val outer2 = "outer2"
            f = fn()
                print(outer1)
                print(outer2)
            end
        end call
    end call
    f call
    // expect: outer1
    // expect: outer2
end

// Share variable across multiple closures.
do
    val append, display = do
        var value = "value"
        val fn1 = fn()
            value = value + "!"
        end
        val fn2 = fn()
            print(value)
        end
        fn1, fn2
    end

    display call // expect: value
    append call
    display call // expect: value!
end
