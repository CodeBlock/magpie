// Evaluate the value expression once.
match print("a") // expect: a
    case "a" then print("one") // expect: one
    case "b" then print("two")
end

// Return the value of the matched case.
print(match 2
    case 1 then "one"
    case 2 then "two"
    case 3 then "three"
end) // expect: two

// Only evaluate the first matched case.
match true
    case true then print("1") // expect: 1
    case true then print("2")
end

// Throw an error if no case matches.
do
    match "not found"
        case "a" then "bad"
        case "b" then "bad"
    end
catch is NoMatchError then
    print("caught") // expect: caught
end

// Bind a variable pattern.
match 1
    case a then print(a) // expect: 1
end

// Scope a binding to just the matched case.
do
    val a = "outer"
    match "match"
        case a then print(a) // expect: match
    end
    print(a) // expect: outer
end

// Allow a block for a case body.
match "first"
    case "first" then
        print("a") // expect: a
        print("b") // expect: b
    case "second" then
        print("bad")
    end
end

// Match a bool literal.
match true
    case true then print("yes") // expect: yes
    case false then print("no")
end

// Match an int literal.
match 2
    case 1 then print("no")
    case 2 then print("yes") // expect: yes
end

// Match a string literal.
match "b"
    case "a" then print("no")
    case "b" then print("yes") // expect: yes
end

// Match a variable pattern with an inner pattern.
match 123
    case a is Bool then print("no")
    case a is Num then print("yes") // expect: yes
end

// Match a class object as a value.
match Num
    case == Bool then print("no")
    case == Num then print("yes") // expect: yes
end

// Allow a single-line else.
match "second"
    case "first" then print("no")
    else print("else") // expect: else
end

// Return the value of the else clause.
print(match "second"
    case "first" then print("no")
    else "else"
end) // expect: else

// Allow a block for the else body.
var result = ""
match "second"
    case "first" then print("no")
    else
        print("a") // expect: a
        print("b") // expect: b
    end
end

// Return the value of the else block.
print(match "second"
    case "first" then print("no")
    else
        "dummy"
        "else"
    end
end) // expect: else
